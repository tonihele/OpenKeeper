apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'idea'

ext {
    jmonkeyengine_version = '[3.1,)'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://jitpack.io" }
    maven { url "http://nifty-gui.sourceforge.net/nifty-maven-repo" }
}

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.
if (!hasProperty('mainClass')) {
    ext.mainClass = 'toniarts.openkeeper.Main'
}
mainClassName = ext.mainClass

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task(debug, dependsOn: classes, type: JavaExec) {
    main = getProperty('mainClass')
    classpath = sourceSets.main.runtimeClasspath
    if (project.hasProperty('args')) {
        args(project.getAt('args').split(','))
    }
    standardInput = System.in
    ignoreExitValue = true
    debug true
}

artifacts {
    archives sourcesJar
    //	archives javadocJar
}

dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])
    compile "org.jmonkeyengine:jme3-core:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-plugins:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-effects:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-networking:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-blender:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-bullet:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-bullet-native:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-niftygui:$jmonkeyengine_version"
    compile "com.badlogicgames.gdx:gdx-ai:1.8.1"
    compile "javax.vecmath:vecmath:1.5.2"
    compile "com.simsilica:zay-es:1.2.1"
    compile "com.simsilica:zay-es-net:1.2.1"
    compile "com.simsilica:sio2:1.1.0"
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDirs 'assets', 'src'
            exclude 'Converted/**'
        }
    }
}


eclipse {
    classpath {
        downloadSources=true
    }
    jdt {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    project {
        natures 'org.springsource.ide.eclipse.gradle.core.nature'
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

compileJava {
    sourceCompatibility = '1.8'
    options.incremental = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

//tasks.withType(JavaCompile) {
//    options.compilerArgs << "-Xlint:deprecation"
//}
